# Keyboard input event.
#
# Contains the key pressed and any modifier keys that were held.
#
# Example:
# ```
# event = termisu.poll_event
# if event.is_a?(Termisu::Event::Key)
#   if event.ctrl_c?
#     puts "Ctrl+C pressed, exiting..."
#   elsif event.key.escape?
#     puts "Escape pressed"
#   end
# end
# ```
struct Termisu::Event::Key
  # The key that was pressed.
  getter key : Input::Key

  # Modifier keys held during the keypress.
  getter modifiers : Input::Modifier

  def initialize(@key : Input::Key, @modifiers : Input::Modifier = Input::Modifier::None)
  end

  # Returns true if Ctrl modifier was held.
  def ctrl? : Bool
    modifiers.ctrl?
  end

  # Returns true if Alt modifier was held.
  def alt? : Bool
    modifiers.alt?
  end

  # Returns true if Shift modifier was held.
  def shift? : Bool
    modifiers.shift?
  end

  # Returns true if Meta modifier was held.
  def meta? : Bool
    modifiers.meta?
  end

  # Returns true if this is Ctrl+C.
  def ctrl_c? : Bool
    ctrl_plain_key?(&.lower_c?)
  end

  # Returns true if this is Ctrl+D.
  def ctrl_d? : Bool
    ctrl_plain_key?(&.lower_d?)
  end

  # Returns true if this is Ctrl+Z.
  def ctrl_z? : Bool
    ctrl_plain_key?(&.lower_z?)
  end

  # Returns true if this is Ctrl+Q.
  def ctrl_q? : Bool
    ctrl_plain_key?(&.lower_q?)
  end

  # Returns the character for this key, if printable.
  def char : Char?
    key.to_char
  end

  private def ctrl_plain_key?(& : Input::Key -> Bool) : Bool
    yield(key) && ctrl? && !alt? && !shift?
  end
end
